; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#define AppName "Remote Access Interface Tester (RAIT)"
#define AppVersion "1.00"
#define AppPublisher "Simulation Systems Ltd"
#define AppURL "https://www.simulation-systems.co.uk"

[Setup]
; NOTE: The value of AppId uniquely identifies this application (not version)
; Do not use the same AppId value in installers for other applications.
AppId={{EF51D443-FA81-4076-991E-B131EF729698}
AppName={#AppName}
AppVersion={#AppVersion}
AppVerName={#AppName} {#AppVersion}
AppPublisher={#AppPublisher}
AppPublisherURL={#AppURL}
AppSupportURL={#AppURL}
AppUpdatesURL={#AppURL}
DefaultDirName={userappdata}\{#AppName}
DisableDirPage=yes
AlwaysShowDirOnReadyPage=no
DefaultGroupName=RAIT
OutputDir="..\..\..\target\"
OutputBaseFilename=setup
SetupIconFile=highways_england_icon.ico
Compression=lzma2
PrivilegesRequired=admin
ChangesAssociations=yes
ArchitecturesInstallIn64BitMode=x64
UninstallDisplayIcon={uninstallexe}
LicenseFile=License(Eula).rtf

[Code]

function CheckTheSelectedUserPath(Param:String): String;
begin
  Result := ExpandConstant('{userappdata}\RAIT')
end;

[Files]
Source: "highways_england_icon.ico"; DestDir: "{code:CheckTheSelectedUserPath}"; Flags: ignoreversion
Source: "launcher.properties"; DestDir: "{code:CheckTheSelectedUserPath}"; Flags: ignoreversion
Source: "..\..\..\target\730_rait-launcher.jar"; DestDir: "{code:CheckTheSelectedUserPath}"; Flags: ignoreversion
Source: "..\..\..\target\730_rait-webapp.jar"; DestDir: "{code:CheckTheSelectedUserPath}"; Flags: ignoreversion
Source: "..\..\..\target\jre\*"; DestDir: "{code:CheckTheSelectedUserPath}\jre"; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "..\..\..\target\wireshark*.exe"; DestDir: {tmp}; Flags: deleteafterinstall; AfterInstall: InstallWireshark;
Source: "..\..\..\target\chrome-win32\*"; DestDir: "{code:CheckTheSelectedUserPath}\chromium"; Flags: ignoreversion recursesubdirs
; NOTE: Don't use "Flags: ignoreversion" on any shared system files

[Code]
procedure InstallWireshark;
var
  ResultCode: Integer;
begin
  // Launch Wireshark installer and wait for it to terminate
  if not Exec(ExpandConstant('{tmp}\wireshark-win32.exe'), '', '', SW_SHOW, ewWaitUntilTerminated, ResultCode) then
  begin
    // you can interact with the user that the installation failed
    MsgBox('Wireshark installation failed with code ' + IntToStr(ResultCode) + ':' + #13#10 +
      SysErrorMessage(ResultCode) + '.', mbError, MB_OK);
  end;
end;

[Languages]
Name: "english"; MessagesFile: "compiler:Default.isl"

[Messages]
WizardSelectTasks=Select Addtional Tasks
SelectTasksDesc=
SelectTasksLabel2=
SelectStartMenuFolderDesc=
SelectStartMenuFolderBrowseLabel=

[Tasks]
Name: "desktopicon"; Description: "Create a &desktop icon";

[Icons]
Name: "{group}\RAIT"; IconFilename: "{code:CheckTheSelectedUserPath}\highways_england_icon.ico"; Filename: "{code:CheckTheSelectedUserPath}\jre\bin\javaw.exe"; WorkingDir: "{code:CheckTheSelectedUserPath}" ;Parameters: "-jar 730_rait-launcher.jar"
Name: "{group}\Uninstall RAIT"; Filename: "{uninstallexe}"
Name: "{commondesktop}\RAIT"; IconFilename: "{code:CheckTheSelectedUserPath}\highways_england_icon.ico"; Filename: "{code:CheckTheSelectedUserPath}\jre\bin\javaw.exe"; WorkingDir: "{code:CheckTheSelectedUserPath}" ;Parameters: "-jar 730_rait-launcher.jar"; Tasks: desktopicon

[Run]
Filename: "{code:CheckTheSelectedUserPath}\jre\bin\javaw.exe"; Description: "{cm:LaunchProgram,Remote Access Interface Tester (RAIT)}"; WorkingDir: "{code:CheckTheSelectedUserPath}"; Parameters: "-jar 730_rait-launcher.jar " ;Flags: unchecked nowait postinstall skipifsilent