SECTIONS
{
    /*
    ** User Code and Library Code
    **
    ** This section must not be assigned to __CODE_BASE,
    ** because CodeGuard(tm) sections may be located there.
    **
    ** Note that input sections *(.text) are not mapped here.
    ** The best-fit allocator locates them, so that .text
    ** may flow around PSV sections as needed.
    */
    .text :
    {
        KEEP (*(.handle));
        *(.init);
        *(.user_init);
        KEEP (*(.isr*));
        *(.libc) *(.libm) *(.libdsp);  /* keep together in this order */
        *(.lib*);
    } >handle

    .program :
    {
        *(main);
    } >program

    .const :
    {
        *(.const)
    } >constants



    /*
    ** Configuration Words
    */
    __CONFIG4 :
    { KEEP (*(__CONFIG4.sec*))    } >CONFIG4
    __CONFIG3 :
    { KEEP (*(__CONFIG3.sec*))    } >CONFIG3
    __CONFIG2 :
    { KEEP (*(__CONFIG2.sec*))    } >CONFIG2
    __CONFIG1 :
    { KEEP (*(__CONFIG1.sec*))    } >CONFIG1


    /*
    ** =========== Data Memory ===========
    */

    /*Data memory */
    __DATA_BASE = 0x880;

    /*
    ** ICD Debug Exec
    **
    ** This section provides optional storage for
    ** the ICD2 debugger. Define a global symbol
    ** named __ICD2RAM to enable ICD2. This section
    ** must be loaded at data address 0x800.
    */
    .icd __DATA_BASE (NOLOAD):
    {
        . += 0x80;
    } > icd_data

    .data :
    {
        *(.data)
    } >static_data

    .bss :
    {
        *(.bss)
    } >bss


    /*
    ** Other sections in data memory are not explicitly mapped.
    ** Instead they are allocated according to their section
    ** attributes, which is most efficient.
    **
    ** If a specific arrangement of sections is required
    ** (other than what can be achieved using attributes)
    ** additional sections may be defined here. See chapter
    ** 10.5 in the MPLAB ASM30/LINK30 User's Guide (DS51317)
    ** for more information.
    */


    /*
    ** ========== Debug Info ==============
    */

    .comment        0 : { *(.comment) }

    /*
    ** DWARF-2
    */
    .debug_info     0 : { *(.debug_info) *(.gnu.linkonce.wi.*) }
    .debug_abbrev   0 : { *(.debug_abbrev) }
    .debug_line     0 : { *(.debug_line) }
    .debug_frame    0 : { *(.debug_frame) }
    .debug_str      0 : { *(.debug_str) }
    .debug_loc      0 : { *(.debug_loc) }
    .debug_macinfo  0 : { *(.debug_macinfo) }
    .debug_pubnames 0 : { *(.debug_pubnames) }
    .debug_ranges   0 : { *(.debug_ranges) }
    .debug_aranges  0 : { *(.debug_aranges) }

    /*
    ** Section added to explicitly align the __c30_signature and __c30_info
    ** sections on 16 bit boundaries in the program memory.
    */
    __c30_signature :
    {
        *(__c30_signature);
    } >program

    __c30_info :
    {
        *(__c30_info);
    } >program

} /* SECTIONS */


__CONFIG4 = 0x2ABF8;
__CONFIG3 = 0x2ABFA;
__CONFIG2 = 0x2ABFC;
__CONFIG1 = 0x2ABFE;
